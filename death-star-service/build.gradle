buildscript {
    ext.kotlin_version = "1.3.72"
    ext.coroutines_version = "1.3.3"
    ext.protobuf_version = "3.11.1"
    ext.grpc_version = "1.28.1"
    ext.grpc_kotlin_version = "0.1.1"

    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'com.google.protobuf:protobuf-gradle-plugin:0.8.5'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}
repositories {
    google()
    jcenter()
    mavenCentral()
    mavenLocal()
}

apply plugin: "kotlin"
apply plugin: "com.google.protobuf"
apply plugin: "idea"
apply plugin: "application"

group 'pl.chacz'
version '1.0-SNAPSHOT'

dependencies {
    // This repo
    implementation "io.grpc:grpc-kotlin-stub:$grpc_kotlin_version"

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$coroutines_version"

    // Grpc and Protobuf
    implementation "com.google.protobuf:protobuf-java:$protobuf_version"
    implementation "com.google.protobuf:protobuf-java-util:$protobuf_version"
    implementation "io.grpc:grpc-netty-shaded:$grpc_version"
    implementation "io.grpc:grpc-protobuf:$grpc_version"
    implementation "io.grpc:grpc-stub:$grpc_version"

    // Java
    compileOnly "javax.annotation:javax.annotation-api:1.2"

    // Google
    implementation "com.google.guava:guava:28.2-jre"
}

protobuf {
    protoc { artifact = "com.google.protobuf:protoc:$protobuf_version" }
    plugins {
        grpc { artifact = "io.grpc:protoc-gen-grpc-java:$grpc_version" }
        grpckt { artifact = "io.grpc:protoc-gen-grpc-kotlin:$grpc_kotlin_version" }
    }
    generateProtoTasks {
        all().each { task ->
            task.plugins {
                grpc { }
                grpckt { }
            }
        }
    }
}

startScripts.enabled = false

task deathStarServer(type: CreateStartScripts) {
    mainClassName = "pl.chacz.deathstar.DeathStarServer"
    applicationName = "death-star-server"
    outputDir = startScripts.outputDir
    classpath = startScripts.classpath
}

applicationDistribution.into("bin") {
    from(deathStarServer)
    fileMode = 0755
}

compileKotlin {
    kotlinOptions.jvmTarget = JavaVersion.VERSION_11
}
compileTestKotlin {
    kotlinOptions.jvmTarget = JavaVersion.VERSION_11
}
